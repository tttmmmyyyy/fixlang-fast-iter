module Test;
import StaticIter;

test_range : IO ();
test_range = (
    println $ "\n[test_range]";;

    let n = 100000;
    let ans = n * (n - 1) / 2;

    let (res, time) = consumed_time_while_lazy(|_|
        StaticIter::range(0, n).fold(0, |sum, i| sum + i)
    );
    assert_eq(|_|"sum_by_fold", res, ans);;
    println("sum_by_fold: " + time.to_string);;

    let (res, time) = consumed_time_while_lazy(|_|
        Iterator::range(0, n).fold(0, |sum, i| sum + i)
    );
    println("sum_by_fold_old: " + time.to_string);;

    pure()
);

test_loop_iter : IO ();
test_loop_iter = (
    println $ "\n[test_loop_iter]";;

    let n = 100000;
    let ans = n * (n - 1) / 2;

    let (res, time) = consumed_time_while_lazy(|_|
        StaticIter::range(0, 2*n).loop_iter(0, |i, sum| if i == n { break(sum) } else { continue(sum + i) })
    );
    assert_eq(|_|"sum_by_loop_iter", res, ans);;
    println("sum_by_loop_iter: " + time.to_string);;

    let (res, time) = consumed_time_while_lazy(|_|
        Iterator::range(0, 2*n).loop_iter(0, |sum, i| if i == n { break(sum) } else { continue(sum + i) })
    );
    println("sum_by_loop_iter_old: " + time.to_string);;

    pure()
);

test_to_array_from_array : IO ();
test_to_array_from_array = (
    println $ "\n[test_to_array_from_array]";;

    let n = 1000;

    let (res, time) = consumed_time_while_lazy(|_|
        StaticIter::range(0, n).to_array
    );
    println("range_to_array: " + time.to_string);;

    let (res, time) = consumed_time_while_lazy(|_|
        Iterator::range(0, n).to_array
    );
    println("range_to_array_old: " + time.to_string);;

    let arr = ["0", "1", "2", "3", "4"];
    assert_eq(|_|"", arr.StaticIter::from_array.to_array, arr);;

    pure()
);

test_flatten : IO ();
test_flatten = (
    println $ "\n[test_flatten]";;

    assert_eq(|_|"", [[1, 2, 3], [], [4, 5, 6]].StaticIter::from_array.map(StaticIter::from_array).flatten.to_array, [1, 2, 3, 4, 5, 6]);;
    assert_eq(|_|"", [[]].StaticIter::from_array.map(StaticIter::from_array).flatten.to_array, [] : Array I64);;
    assert_eq(|_|"", [].StaticIter::from_array.map(StaticIter::from_array).flatten.to_array, [] : Array I64);;
    assert_eq(|_|"", StaticIter::from_array([StaticIter::range(1, 4), range(4, 4), range(4, 7)]).flatten.to_array, StaticIter::range(1, 7).to_array);;

    pure()
);

test_map_filter : IO ();
test_map_filter = (
    println $ "\n[test_map_filter]";;

    let n = 100000;
    let ans = n/3 + n/5 - n/15;

    let (res, time) = consumed_time_while_lazy(|_|
        StaticIter::range(0, n).filter(|i| i % 3 == 0 || i % 5 == 0).map(|i| 1).fold(0, |i, sum| sum + i)
    );
    assert_eq(|_|"count_div_by_3_or_5", res, ans);;
    println("count_div_by_3_or_5: " + time.to_string);;

    let (res, time) = consumed_time_while_lazy(|_|
        Iterator::range(0, n).filter(|i| i % 3 == 0 || i % 5 == 0).map(|i| 1).fold(0, |sum, i| sum + i)
    );
    println("count_div_by_3_or_5_old: " + time.to_string);;

    pure()
);

test_product : IO ();
test_product = (
    println $ "\n[test_product]";;

    assert_eq(|_|"", StaticIter::range(1, 4).product(['a', 'b'].from_array).to_array, [(1, 'a'), (2, 'a'), (3, 'a'), (1, 'b'), (2, 'b'), (3, 'b')]);;

    let n = 50;
    let (res1, time) = consumed_time_while_lazy(|_|
        let r1 = StaticIter::range(1, n);
        let r2 = StaticIter::range(1, 2*n*n);
        r1.product(r1).product(r2).filter(|((a, b), c)| a*a + b*b == c*c).fold(0, |_, sum| sum + 1)
    );
    println("pythagorean_triple: " + time.to_string);;

    let (res2, time) = consumed_time_while_lazy(|_|
        let r1 = Iterator::range(1, n);
        let r2 = Iterator::range(1, 2*n*n);
        r1.product(r1).product(r2).filter(|((a, b), c)| a*a + b*b == c*c).fold(0, |sum, _| sum + 1)
    );
    println("pythagorean_triple_old: " + time.to_string);;
    assert_eq(|_|"pythagorean_triple", res1, res2);;

    pure()
);

test_get_size : IO ();
test_get_size = (
    println $ "\n[test_get_size]";;

    assert_eq(|_|"", StaticIter::range(1, 4).get_size, 3);;

    pure()
);

test_empty : IO ();
test_empty = (
    println $ "\n[test_empty]";;

    assert_eq(|_|"", (StaticIter::empty : EmptyIterator I64).get_size, 0);;

    pure()
);

test_get_first : IO ();
test_get_first = (
    println $ "\n[test_get_first]";;

    assert_eq(|_|"", StaticIter::range(1, 4).get_front, 1);;

    pure()
);

test_remove_first : IO ();
test_remove_first = (
    println $ "\n[test_remove_first]";;

    assert_eq(|_|"", StaticIter::range(1, 4).pop_front.to_array, [2, 3]);;
    assert_eq(|_|"", StaticIter::range(0, 0).pop_front.to_array, []);;

    pure()
);

test_is_empty : IO ();
test_is_empty = (
    println $ "\n[test_is_empty]";;

    assert_eq(|_|"", StaticIter::range(1, 4).is_empty, false);;
    assert_eq(|_|"", StaticIter::range(0, 0).is_empty, true);;

    pure()
);

test_fold_m : IO ();
test_fold_m = (
    println $ "\n[test_fold_m]";;

    StaticIter::range(0, 10).fold_m(0, |i, sum|
        let sum = sum + i;
        println $ "sum up to " + i.to_string + ": " + sum.to_string;;
        pure $ sum
    );;

    pure()
);

test_loop_iter_m : IO ();
test_loop_iter_m = (
    println $ "\n[test_loop_iter_m]";;

    StaticIter::range(0, 20).loop_iter_m(0, |i, sum|
        if i == 10 { break_m $ sum };
        let sum = sum + i;
        println $ "sum up to " + i.to_string + ": " + sum.to_string;;
        continue_m $ sum
    );;

    pure()
);

test_generate : IO ();
test_generate = (
    println $ "\n[test_generate]";;

    let iter = StaticIter::generate(0, |_| Option::none());
    assert_eq(|_|"", iter.to_array, [] : Array I64);;

    let iter = Iterator::generate(0, |i| if i == 3 { Option::none() } else { Option::some $ (i, i+1) });
    assert_eq(|_|"", iter.to_array, [0, 1, 2]);;

    pure()
);

test_intersperse : IO ();
test_intersperse = (
    println $ "\n[test_intersperse]";;

    assert_eq(|_|"", StaticIter::range(1, 4).intersperse(0).to_array, [1, 0, 2, 0, 3]);;
    assert_eq(|_|"", StaticIter::range(1, 2).intersperse(0).to_array, [1]);;
    assert_eq(|_|"", StaticIter::range(1, 1).intersperse(0).to_array, [] : Array I64);;

    pure()
);

test_push_front : IO ();
test_push_front = (
    println $ "\n[test_push_front]";;

    let iter = StaticIter::empty.push_front(3).push_front(2).push_front(1).push_front(0);
    assert_eq(|_|"", iter.to_array, [0, 1, 2, 3]);;

    pure()
);

test_take : IO ();
test_take = (
    println $ "\n[test_take]";;

    assert_eq(|_|"", StaticIter::range(1, 4).take(2).to_array, [1, 2]);;
    assert_eq(|_|"", StaticIter::range(1, 4).take(0).to_array, [] : Array I64);;

    pure()
);

test_take_while : IO ();
test_take_while = (
    println $ "\n[test_take_while]";;

    assert_eq(|_|"", StaticIter::range(1, 4).take_while(|i| i < 3).to_array, [1, 2]);;
    assert_eq(|_|"", StaticIter::range(1, 4).take_while(|i| i < 1).to_array, [] : Array I64);;

    pure()
);

test_zip : IO ();
test_zip = (
    println $ "\n[test_zip]";;

    let iter = StaticIter::range(1, 4).zip(StaticIter::range(4, 10));
    assert_eq(|_|"", iter.to_array, [(1, 4), (2, 5), (3, 6)]);;

    pure()
);

test_monad : IO ();
test_monad = (
    println $ "\n[test_monad]";;

    let iter = do {
        let x = *StaticIter::range(1, 4).to_dyn;
        let y = *['A', 'B'].StaticIter::from_array.to_dyn;
        pure $ (x, y)
    };

    pure()
);

test : IO ();
test = (
    test_range;;
    test_loop_iter;;
    test_to_array_from_array;;
    test_map_filter;;
    test_flatten;;
    test_product;;
    test_get_size;;
    test_empty;;
    test_get_first;;
    test_remove_first;;
    test_is_empty;;
    test_fold_m;;
    test_loop_iter_m;;
    test_generate;;
    test_intersperse;;
    test_push_front;;
    test_take;;
    test_take_while;;
    test_zip;;
    test_monad;;

    pure()
);